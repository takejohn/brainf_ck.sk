options:
    MAX_STEPS: 100000  # 1回のプログラムで実行できるステップ数の上限。

variables:
    {brainf_ck.charcodes::32}  = " "
    {brainf_ck.charcodes::33}  = "!"
    {brainf_ck.charcodes::34}  = """"
    {brainf_ck.charcodes::35}  = "##"
    {brainf_ck.charcodes::36}  = "$"
    {brainf_ck.charcodes::37}  = "%"
    {brainf_ck.charcodes::38}  = "&"
    {brainf_ck.charcodes::39}  = "'"
    {brainf_ck.charcodes::40}  = "("
    {brainf_ck.charcodes::41}  = ")"
    {brainf_ck.charcodes::42}  = "*"
    {brainf_ck.charcodes::43}  = "+"
    {brainf_ck.charcodes::44}  = ","
    {brainf_ck.charcodes::45}  = "-"
    {brainf_ck.charcodes::46}  = "."
    {brainf_ck.charcodes::47}  = "/"
    {brainf_ck.charcodes::48}  = "0"
    {brainf_ck.charcodes::49}  = "1"
    {brainf_ck.charcodes::50}  = "2"
    {brainf_ck.charcodes::51}  = "3"
    {brainf_ck.charcodes::52}  = "4"
    {brainf_ck.charcodes::53}  = "5"
    {brainf_ck.charcodes::54}  = "6"
    {brainf_ck.charcodes::55}  = "7"
    {brainf_ck.charcodes::56}  = "8"
    {brainf_ck.charcodes::57}  = "9"
    {brainf_ck.charcodes::58}  = ":"
    {brainf_ck.charcodes::59}  = ";"
    {brainf_ck.charcodes::60}  = "<"
    {brainf_ck.charcodes::61}  = "="
    {brainf_ck.charcodes::62}  = ">"
    {brainf_ck.charcodes::63}  = "?"
    {brainf_ck.charcodes::64}  = "@"
    {brainf_ck.charcodes::65}  = "A"
    {brainf_ck.charcodes::66}  = "B"
    {brainf_ck.charcodes::67}  = "C"
    {brainf_ck.charcodes::68}  = "D"
    {brainf_ck.charcodes::69}  = "E"
    {brainf_ck.charcodes::70}  = "F"
    {brainf_ck.charcodes::71}  = "G"
    {brainf_ck.charcodes::72}  = "H"
    {brainf_ck.charcodes::73}  = "I"
    {brainf_ck.charcodes::74}  = "J"
    {brainf_ck.charcodes::75}  = "K"
    {brainf_ck.charcodes::76}  = "L"
    {brainf_ck.charcodes::77}  = "M"
    {brainf_ck.charcodes::78}  = "N"
    {brainf_ck.charcodes::79}  = "O"
    {brainf_ck.charcodes::80}  = "P"
    {brainf_ck.charcodes::81}  = "Q"
    {brainf_ck.charcodes::82}  = "R"
    {brainf_ck.charcodes::83}  = "S"
    {brainf_ck.charcodes::84}  = "T"
    {brainf_ck.charcodes::85}  = "U"
    {brainf_ck.charcodes::86}  = "V"
    {brainf_ck.charcodes::87}  = "W"
    {brainf_ck.charcodes::88}  = "X"
    {brainf_ck.charcodes::89}  = "Y"
    {brainf_ck.charcodes::90}  = "Z"
    {brainf_ck.charcodes::91}  = "["
    {brainf_ck.charcodes::92}  = "\"
    {brainf_ck.charcodes::93}  = "]"
    {brainf_ck.charcodes::94}  = "^"
    {brainf_ck.charcodes::95}  = "_"
    {brainf_ck.charcodes::96}  = "`"
    {brainf_ck.charcodes::97}  = "a"
    {brainf_ck.charcodes::98}  = "b"
    {brainf_ck.charcodes::99}  = "c"
    {brainf_ck.charcodes::100} = "d"
    {brainf_ck.charcodes::101} = "e"
    {brainf_ck.charcodes::102} = "f"
    {brainf_ck.charcodes::103} = "g"
    {brainf_ck.charcodes::104} = "h"
    {brainf_ck.charcodes::105} = "i"
    {brainf_ck.charcodes::106} = "j"
    {brainf_ck.charcodes::107} = "k"
    {brainf_ck.charcodes::108} = "l"
    {brainf_ck.charcodes::109} = "m"
    {brainf_ck.charcodes::110} = "n"
    {brainf_ck.charcodes::111} = "o"
    {brainf_ck.charcodes::112} = "p"
    {brainf_ck.charcodes::113} = "q"
    {brainf_ck.charcodes::114} = "r"
    {brainf_ck.charcodes::115} = "s"
    {brainf_ck.charcodes::116} = "t"
    {brainf_ck.charcodes::117} = "u"
    {brainf_ck.charcodes::118} = "v"
    {brainf_ck.charcodes::119} = "w"
    {brainf_ck.charcodes::120} = "x"
    {brainf_ck.charcodes::121} = "y"
    {brainf_ck.charcodes::122} = "z"
    {brainf_ck.charcodes::123} = "{"
    {brainf_ck.charcodes::124} = "|"
    {brainf_ck.charcodes::125} = "}"
    {brainf_ck.charcodes::126} = "~"

    {brainf_ck.hex_digits::0}  = "0"
    {brainf_ck.hex_digits::1}  = "1"
    {brainf_ck.hex_digits::2}  = "2"
    {brainf_ck.hex_digits::3}  = "3"
    {brainf_ck.hex_digits::4}  = "4"
    {brainf_ck.hex_digits::5}  = "5"
    {brainf_ck.hex_digits::6}  = "6"
    {brainf_ck.hex_digits::7}  = "7"
    {brainf_ck.hex_digits::8}  = "8"
    {brainf_ck.hex_digits::9}  = "9"
    {brainf_ck.hex_digits::10} = "a"
    {brainf_ck.hex_digits::11} = "b"
    {brainf_ck.hex_digits::12} = "c"
    {brainf_ck.hex_digits::13} = "d"
    {brainf_ck.hex_digits::14} = "e"
    {brainf_ck.hex_digits::15} = "f"

# Brainf*ck のプログラムを実行するコマンド。
# 1つ目の引数はプログラムのコード。
# 2つ目の引数はプログラムに渡す入力 (任意)。
command /bf <text> <text = "">:
    trigger:
        set {_program::*} to split arg 1 at ""
        set {_pc} to 1
        set {_memory::*} to 0
        set {_pointer} to 1
        set {_input::*} to split arg 2 at ""
        set {_input-index} to 1
        set {_steps} to 0

        while {_pc} is less than size of {_program::*}:
            if {_steps} is greater than or equal to {@MAX_STEPS}:
                send "&cERROR: At %{_pc}%: Program timeout" to sender
                exit loop

            set {_instruction} to {_program::%{_pc}%}

            if {_instruction} is "?":
                send "Memory: At %{_pc}%: {%{_memory::*}%}" to sender
                add 1 to {_pc}
            else if {_instruction} is "+":
                set {_memory::%{_pointer}%} to plus({_memory::%{_pointer}%})
                add 1 to {_pc}
            else if {_instruction} is "-":
                set {_memory::%{_pointer}%} to minus({_memory::%{_pointer}%})
                add 1 to {_pc}
            else if {_instruction} is ">":
                add 1 to {_pointer}
                if {_memory::%{_pointer}%} is not set:
                    set {_memory::%{_pointer}%} to 0
                add 1 to {_pc}
            else if {_instruction} is "<":
                subtract 1 from {_pointer}
                if {_pointer} is less than 1:
                    send "&cERROR: At %{_pc}%: Pointer out of bound" to sender
                    exit loop
                add 1 to {_pc}
            else if {_instruction} is ".":
                add {_memory::%{_pointer}%} to {_output::*}
                add 1 to {_pc}
            else if {_instruction} is ",":
                if {_input-index} is less than size of {_input::*}:
                    set {_memory::%{_pointer}%} to to_code_point({_input::%{_input-index}%})
                    add 1 to {_input-index}
                else:
                    set {_memory::%{_pointer}%} to 0
                add 1 to {_pc}
            else if {_instruction} is "[":
                if {_jump-addresses::%{_pc}%} is set:
                    set {_jump-address} to {_jump-addresses::%{_pc}%}
                else:
                    set {_jump-address} to get_jump_address({_program::*}, {_pc})
                    set {_jump-addresses::%{_pc}%} to {_jump-address}
                    set {_jump-addresses::%{_jump-address}%} to {_pc}
                if {_jump-address} is less than 0:
                    send "&cERROR: At %{_pc}%: Corresponding right bracket is missing" to sender
                    exit loop
                if {_memory::%{_pointer}%} is 0:
                    set {_pc} to {_jump-address} + 1
                else:
                    add 1 to {_pc}
            else if {_instruction} is "]":
                if {_jump-addresses::%{_pc}%} is not set:
                    send "&cERROR: At %{_pc}%: Corresponding left bracket is missing" to sender
                    exit loop
                set {_pc} to {_jump-addresses::%{_pc}%}
            else:
                add 1 to {_pc}
                continue loop

            add 1 to {_steps}

        send from_code_points({_output::*}) to sender

# ラップアラウンド演算において8ビット符号なし整数に1を足す。
function plus(x: integer) :: integer:
    if {_x} is equal to 255:
        set {_x} to 0
    else:
        add 1 to {_x}
    return {_x}

# ラップアラウンド演算において8ビット符号なし整数から1を引く。
function minus(x: integer) :: integer:
    if {_x} is equal to 0:
        set {_x} to 255
    else:
        subtract 1 from {_x}
    return {_x}

# 文字から文字コードに変換する。
function to_code_point(c: text) :: integer:
    if {_c} matches "[A-Z]":
        set {_result} to 65
        while {_result} is less than or equal to 90:
            if {brainf_ck.charcodes::%{_result}%} is {_c}:
                return {_result}
            add 1 to {_result}
    if {_c} matches "[a-z]":
        set {_result} to 97
        while {_result} is less than or equal to 122:
            if {brainf_ck.charcodes::%{_result}%} is {_c}:
                return {_result}
            add 1 to {_result}
    set {_result} to 32
    while {_result} is less than or equal to 126:
        if {brainf_ck.charcodes::%{_result}%} is {_c}:
            return {_result}
        add 1 to {_result}
    return 0

# 文字コードのリストから文字列に変換する。
function from_code_points(code-points: integers) :: text:
    loop {_code-points::*}:
        if {brainf_ck.charcodes::%loop-value%} is set:
            add {brainf_ck.charcodes::%loop-value%} to {_characters::*}
        else if loop-value is 10:
            add nl to {_characters::*}
        else:
            add hex(loop-value) to {_characters::*}
    set {_result} to join {_characters::*} with ""
    return {_result}

# 整数値を2桁の16進数で表す文字列を返す。
function hex(code: integer) :: text:
    set {_high} to floor({_code} / 16)
    set {_high-char} to {brainf_ck.hex_digits::%{_high}%}
    set {_low} to mod({_code}, 16)
    set {_low-char} to {brainf_ck.hex_digits::%{_low}%}
    return "&e<%{_high-char}%%{_low-char}%>&r"

# プログラム中の左括弧 "[" に対応する右括弧 "]" が何個目の文字であるかを求める。
# 右括弧が見つからなかった場合は-1を返す。
function get_jump_address(prog: texts, pc: integer) :: integer:
    set {_nesting-depth} to 0
    set {_result} to {_pc}
    while {_result} is less than size of {_prog::*}:
        set {_instruction} to {_prog::%{_result}%}
        if {_instruction} is "[":
            add 1 to {_nesting-depth}
        else if {_instruction} is "]":
            subtract 1 from {_nesting-depth}
            if {_nesting-depth} is 0:
                return {_result}
        add 1 to {_result}
    return -1
